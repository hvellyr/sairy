;;; Copyright (c) 2017 Gregor Klinke
;;; All rights reserved.

(mode decl-mode
  (element prm
    (make sequence
      font-posture: 'oblique
      font-weight: 'medium
      (process-node-list (attribute "text" (current-node)))))

  (element var
    (make sequence
      font-posture: 'oblique
      font-weight: 'medium
      (process-node-list (attribute "text" (current-node)))))

  (element ?
    (sosofo-append
     (make sequence
       font-weight: 'medium
       (literal "["))
     (process-node-list (attribute "optionalexpr" (current-node)))
     (make sequence
       font-weight: 'medium
       (literal "]"))))

  (element p
    (make paragraph
      space-before: 0pt
      space-after:  0pt
      start-indent: (example-start-indent (parent (current-node)))
      font-name:    %monospace-font-name%
      font-size:    (font-size 2)
      line-spacing: (line-spacing 2)
      font-weight:  'bold
      lines:        'asis
      whitespace-treatment: 'preserve
      (process-children)))
  )

(define (decl-space-before classic-val modern-val)
  (case %style%
    ((classic) (display-space classic-val conditional?: #t))
    ((modern) (display-space modern-val conditional?: #t))
    (else (display-space modern-val conditional?: #t))))

(define (decl-space-after classic-val modern-val)
  (case %style%
    ((classic) (display-space classic-val conditional?: #t))
    ((modern) (display-space modern-val conditional?: #t))
    (else (display-space modern-val conditional?: #t))))

(define (make-def-sosofo nd head-sosofo kind)
  (sosofo-append
   (make anchor
     display?: #t
     id: (id nd))
   (make paragraph
     space-before: (decl-space-before 7pt 7pt)
     space-after: (decl-space-after 7pt 0pt)
     first-line-start-indent: %page-left-offset-neg%
     start-indent: 0mm
     keep-with-next?: #t
     (sosofo-append
      (format-title (make sequence
                      font-posture: 'italic
                      kind)
                    (literal " ")
                    (with-mode decl-mode
                      (make line-field
                        font-name: %monospace-font-name%
                        font-weight: 'bold
                        (process-node-list head-sosofo))))))
   (make display-group
     space-after: (decl-space-after 14pt 7pt)
     (process-node-list (children nd)))))

(element deffn
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Function")))

(element defop
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Operator")))

(element defopt
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Option")))

(element defprop
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Property")))

(element defty
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Type")))

(element deftag
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (literal "Tag")))

(element decl
  (make display-group
    space-before: (display-space 7pt conditional?: #t)
    space-after:  (display-space 7pt conditional?: #t)
    (with-mode decl-mode
      (process-children))))

(element def
  (make-def-sosofo (current-node)
                   (attribute "declln" (current-node))
                   (process-node-list (attribute "kind" (current-node)))))


(element (definitions def)
  (let ((declln (attribute "declln" (current-node)))
        (first-elt (node-list-first (children (current-node))))
        )
    (sosofo-append
     (make anchor
       display?: #t
       id: (id (current-node)))

     (if (or (> (string-length (data declln)) 15)
             (and (not (node-list-empty? first-elt))
                  (not (equal? (gi first-elt) 'p))))
         (sosofo-append
          (make paragraph
            first-line-start-indent: 0pt
            start-indent: 0pt
            quadding: 'justify

            (with-mode decl-mode
              (make sequence
                font-name: %monospace-font-name%
                (process-node-list declln))))
          (make paragraph
            first-line-start-indent: 0pt
            start-indent: 70pt
            quadding: 'justify
            (process-node-list-trim (node-list-first (children (current-node)))))
          )
         (sosofo-append
          (make paragraph
            first-line-start-indent: -70pt
            start-indent: 70pt
            quadding: 'justify
            (sosofo-append
             (make line-field
               font-name: %monospace-font-name%
               ;;font-weight: 'bold
               field-width: (inline-space 70pt max: 'inf)
               field-align: 'left
               (sosofo-append
                (with-mode decl-mode
                  (process-node-list declln))
                (make-char 'non-breaking-space)))
             (process-node-list-trim (children first-elt))))))
     (process-node-list (node-list-rest (children (current-node)))))))
